Shader "Hidden/HDRP/UI_Compositing"
{
    Properties
    {
        _MainTex("Main Texture", 2DArray) = "white" {}
    }

    SubShader
    {
    	PackageRequirements {
            "com.unity.render-pipelines.high-definition": "0.1"
        }
        Tags{ "RenderPipeline" = "HDRenderPipeline" }
        Pass
        {
            Name "HDRP UI Compositing"

            ZWrite Off
            ZTest Always
            Blend SrcAlpha OneMinusSrcAlpha
            Cull Off

            HLSLINCLUDE
            #include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/TextureXR.hlsl"
            #include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"

            TEXTURE2D_X(_MainTex);

            struct Attributes
            {
                uint vertexID : SV_VertexID;
                UNITY_VERTEX_INPUT_INSTANCE_ID
            };

            struct Varyings
            {
                float4 positionCS : SV_POSITION;
                float2 uv : TEXCOORD0;
                UNITY_VERTEX_OUTPUT_STEREO
            };

            Varyings FullscreenVertex(Attributes input)
            {
                Varyings output;
                UNITY_SETUP_INSTANCE_ID(input);
                UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(output);
                output.positionCS = GetFullScreenTriangleVertexPosition(input.vertexID, UNITY_RAW_FAR_CLIP_VALUE);
                output.uv = output.positionCS.xy * 0.5 + 0.5;
                return output;
            }

            float4 Compositing(Varyings varyings) : SV_Target
            {
                UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(varyings);
                float2 uv = varyings.uv;
                return SAMPLE_TEXTURE2D_X_LOD(_MainTex, s_linear_clamp_sampler, uv, 0);
            }
            ENDHLSL
            HLSLPROGRAM
                #pragma fragment Compositing
                #pragma vertex FullscreenVertex
            ENDHLSL
        }
    }
    Fallback Off
}
